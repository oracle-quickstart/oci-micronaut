plugins {
    id("com.github.johnrengelman.shadow") version "7.1.2"
    id("io.micronaut.application") version "3.6.2"
}

group = "micronaut.mushop"

repositories {
    mavenCentral()
}

micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("assets.*")
    }
}

dependencies {
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")

    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-runtime")
    implementation("io.micronaut:micronaut-validation")

    // Metrics
    implementation("io.micronaut:micronaut-management")
    implementation("io.micronaut.micrometer:micronaut-micrometer-core")
    implementation("io.micronaut.micrometer:micronaut-micrometer-registry-prometheus")

    // Tracing
    implementation("io.micronaut.tracing:micronaut-tracing-core")
    implementation("io.micronaut.tracing:micronaut-tracing-zipkin")

    implementation("io.swagger.core.v3:swagger-annotations")

    implementation("io.micronaut.objectstorage:micronaut-object-storage-core:1.1.0")

    runtimeOnly("ch.qos.logback:logback-classic")
}

application {
    mainClass.set("assets.Application")
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

if (javaBaseImage == "graalvm") {
    dockerfile {
        baseImage = "ghcr.io/graalvm/graalvm-ce:ol8-java11-21.1.0"
    }
}

dockerBuild {
    images = ["phx.ocir.io/oraclelabs/micronaut-showcase/mushop/$project.parent.name-$project.name-${javaBaseImage}:$project.version"]
}

dockerBuildNative {
    images = ["phx.ocir.io/oraclelabs/micronaut-showcase/mushop/${project.parent.name}-${project.name}-native:$project.version"]
}

processResources {
    def assetPaths = []
    eachFile {details ->
        if (details.path.contains("assets")) {
            assetPaths << details.path.minus("assets/")
        }
    }
    doLast {
        File assetsFile = new File("$destinationDir/assets/manifest.txt")
        assetsFile.withWriter{ out ->
            assetPaths.each {out.println it}
        }
    }
}
