plugins {
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.micronaut.application") version "4.3.4"
    id("io.micronaut.aot") version "4.3.4"
}

group = "micronaut.mushop"

repositories {
    mavenCentral()
}

micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("assets.*")
    }
    aot {
        optimizeServiceLoading = false
        convertYamlToJava = false
        precomputeOperations = true
        cacheEnvironment = true
        optimizeClassLoading = true
        deduceEnvironment = true
        optimizeNetty = true
    }
}

dependencies {
    implementation(project(":app"))

    // Authentication
    implementation("software.amazon.awssdk:sts")

    // Configuration discovery
    implementation("io.micronaut.aws:micronaut-aws-parameter-store")

    // Metrics
    implementation("io.micronaut.micrometer:micronaut-micrometer-registry-cloudwatch")

    // Object Storage
    implementation("io.micronaut.aws:micronaut-aws-sdk-v2")
    implementation("io.micronaut.objectstorage:micronaut-object-storage-aws")

    testImplementation(project(":tck"))
    testImplementation("org.mockito:mockito-core")
}

application {
    mainClass.set("assets.AwsApplication")
}

java {
    sourceCompatibility = JavaVersion.toVersion("21")
    targetCompatibility = JavaVersion.toVersion("21")
}

if (javaBaseImage == "graalvm") {
    dockerfile {
        baseImage = "ghcr.io/graalvm/native-image-community:21"
    }
}

dockerBuild {
    images = ["phx.ocir.io/oraclelabs/micronaut-showcase/mushop/$project.parent.name-$project.name-${javaBaseImage}:$project.version"]
}

dockerBuildNative {
    images = ["phx.ocir.io/oraclelabs/micronaut-showcase/mushop/${project.parent.name}-${project.name}-native:$project.version"]
}
