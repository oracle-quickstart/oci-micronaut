plugins {
    id("com.github.johnrengelman.shadow") version "6.1.0"
    id("io.micronaut.application") version "1.5.0"
}

version = "1.0.0"
group = "microanut.mushop.newsletter.subscription"

repositories {
    mavenCentral()
}

micronaut {
    runtime("oracle_function")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("newsletter.subscription.*")
    }
}

dependencies {
    implementation("io.micronaut:micronaut-validation")
    implementation("io.micronaut:micronaut-runtime")
    implementation("jakarta.annotation:jakarta.annotation-api")
    implementation("io.micronaut:micronaut-http-client")

    implementation 'javax.mail:mail:1.5.0-b01'
    implementation("io.micronaut.oraclecloud:micronaut-oraclecloud-function") {
        exclude module:'micronaut-oraclecloud-common'
    }

    testImplementation("com.fnproject.fn:testing-junit4")
    runtimeOnly("org.slf4j:slf4j-simple")
}


application {
    mainClass.set("com.example.Application")
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

if (javaBaseImage == "graalvm") {
    dockerfile {
        baseImage = "ghcr.io/graalvm/graalvm-ce:ol8-java11-21.1.0"
    }
}


nativeImage {
    args("--static",
         "--initialize-at-build-time=newsletter.subscription")
}

dockerfileNative {
    args("-XX:MaximumHeapSizePercent=80")
}

dockerBuild {
    images = ["iad.ocir.io/cloudnative-devrel/micronaut-showcase/mushop/$project.name-${javaBaseImage}:$project.version"]
}

dockerBuildNative {
    images = ["iad.ocir.io/cloudnative-devrel/micronaut-showcase/mushop/${project.name}-native:$project.version"]
}
