plugins {
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.micronaut.application") version "4.3.3"
    id("io.micronaut.aot") version "4.3.3"
}

group = "micronaut.mushop"

repositories {
    mavenCentral()
}

micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("user.*")
    }
    aot {
        optimizeServiceLoading = false
        convertYamlToJava = false
        precomputeOperations = true
        cacheEnvironment = true
        optimizeClassLoading = true
        deduceEnvironment = true
        optimizeNetty = true
    }
}

dependencies {
    annotationProcessor("io.micronaut.data:micronaut-data-processor")

    runtimeOnly("org.flywaydb:flyway-database-oracle")
    

    implementation(project(":app")) {
        exclude group:'com.h2database', module:'h2'
    }

    // Metrics
    implementation("com.oracle.oci.sdk:oci-java-sdk-monitoring")
    implementation("io.micronaut.oraclecloud:micronaut-oraclecloud-micrometer")
    implementation("io.micronaut.tracing:micronaut-tracing-opentelemetry-zipkin-exporter")


    // SDK
    implementation("io.micronaut.oraclecloud:micronaut-oraclecloud-sdk")

    // Database
    implementation("io.micronaut.data:micronaut-data-jdbc")
    implementation("io.micronaut.oraclecloud:micronaut-oraclecloud-atp")
    implementation platform("com.oracle.database.jdbc:ojdbc-bom:23.3.0.23.09")
    implementation("io.micronaut.sql:micronaut-jdbc-hikari")
    // implementation("io.micronaut.flyway:micronaut-flyway")

    runtimeOnly("org.flywaydb:flyway-database-oracle")
    runtimeOnly("com.oracle.database.security:oraclepki")
    runtimeOnly("com.oracle.database.security:osdt_cert")
    runtimeOnly("com.oracle.database.security:osdt_core")

    testImplementation("org.testcontainers:oracle-xe")
    testImplementation(project(":tck"))
}

application {
    mainClass.set("user.OciApplication")
}

java {
    sourceCompatibility = JavaVersion.toVersion("21")
    targetCompatibility = JavaVersion.toVersion("21")
}

if (javaBaseImage == "graalvm") {
    dockerfile {
        baseImage = "ghcr.io/graalvm/native-image-community:21"
    }
}

dockerBuild {
    images = ["phx.ocir.io/oraclelabs/micronaut-showcase/mushop/$project.parent.name-$project.name-${javaBaseImage}:$project.version"]
}

dockerBuildNative {
    images = ["phx.ocir.io/oraclelabs/micronaut-showcase/mushop/${project.parent.name}-${project.name}-native:$project.version"]
}

dockerfileNative {
    instruction "RUN echo 'http://dl-cdn.alpinelinux.org/alpine/v3.11/main' >> /etc/apk/repositories && apk update && apk add 'zlib<1.2.12'"
}
